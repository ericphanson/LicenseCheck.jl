module LicenseCheck

using licensecheck_jll: licensecheck_jll

export licensecheck, is_osi_approved
export find_licenses, find_license
export find_licenses_by_bruteforce, find_licenses_by_list,
    find_licenses_by_list_intersection

export clear_license_list, reset_to_builtin_licenses, add_builtin_license, add_license

include("OSI_LICENSES.jl")
include("find_licenses.jl")

"""
    licensecheck(text::String) -> @NamedTuple{licenses_found::Vector{String}, license_file_percent_covered::Float64}

Returns a vector of the names of licenses matched in `text` and the percent of the text covered by these matches.

The check is performed with active license set which can be controlled and default initialized with `reset_to_builtin_licenses`.

This provides some of the functionality of `licensecheck.Scan` in the original Go library ([https://github.com/google/licensecheck](https://github.com/google/licensecheck)).

## Example

```julia
julia> using LicenseCheck

julia> text = read(joinpath(pkgdir(LicenseCheck), "LICENSE"), String);

julia> licensecheck(text)
(licenses_found = ["MIT"], license_file_percent_covered = 98.82352941176471)
```
"""
function licensecheck(text::String)
    arr, dims, license_file_percent_covered = ccall((:License,
            licensecheck_jll.licensecheck),
        Tuple{Ptr{Ptr{UInt8}},Cint,Float64},
        (Cstring,), text)
    return (; licenses_found=unsafe_string.(unsafe_wrap(Array, arr, dims; own=true)),
        license_file_percent_covered=license_file_percent_covered)
end

"""
    is_osi_approved(spdx_identifier::String) -> Bool
    is_osi_approved(nt::NamedTuple) -> Bool

Checks if a [SDPX 3.10 identifier](https://spdx.dev/ids/) is
[OSI approved](https://opensource.org/licenses) by checking against an autogenerated list,
or if a `NamedTuple` with a `licenses_found` key contains only OSI-approved SDPX 3.10 identifiers.

!!! warning
    
    Note that [`licensecheck`](@ref) understands a larger number of licenses than
    SDPX 3.10, and can output results which are not SDPX 3.10 identifiers.
    Such licenses will yield `is_osi_approved(name) == false` regardless of
    their OSI standing.

## Example

```julia
julia> is_osi_approved("MIT")
true

julia> is_osi_approved(find_license(pkgdir(LicenseCheck)))
true

julia> is_osi_approved(find_license(homedir(), allow_brute=false)) # no license there
false

```
"""
is_osi_approved(spdx_identifier::String) = spdx_identifier âˆˆ OSI_LICENSES
function is_osi_approved(nt::NamedTuple)
    return !isempty(nt.licenses_found) && all(is_osi_approved, nt.licenses_found)
end
is_osi_approved(::Nothing) = false # so that it can always be used with `find_license`

"""
    clear_license_list()

Empties active license set.

`licensecheck`` for such case will return empty result.

See also: `add_builtin_license`, `add_license`
"""
function clear_license_list()
    ccall((:ClearLicenseList, licensecheck_jll.licensecheck),
        Nothing,
        ())
    return nothing
end

"""
    reset_to_builtin_licenses()

Reset active license set to the full state of built in licenses.

The full list of license IDs is located at [https://github.com/google/licensecheck/blob/v0.3.1/licenses/README.md](https://github.com/google/licensecheck/blob/v0.3.1/licenses/README.md), and includes the SDPX 3.10 licenses, [https://github.com/spdx/license-list-data/tree/v3.10/text](https://github.com/spdx/license-list-data/tree/v3.10/text).
"""
function reset_to_builtin_licenses()
    ccall((:ResetToBuiltinLicences, licensecheck_jll.licensecheck),
        Nothing,
        ())
    return nothing
end

"""
    add_builtin_license(name::String)

Add one of the built in licenses to the active license set.
See `reset_to_builtin_licenses` to learn which values `name` can take.
Will have no effect or error if `name` is not found.
"""
function add_builtin_license(name::String)
    ccall((:AddBuiltinLicense, licensecheck_jll.licensecheck),
        Nothing,
        (Cstring,), name)
    return nothing
end

"""
    add_license(id::String, license_regular_expression::String)

Adds new license to the active license set with provided `id` and `license_regular_expression`.

Check documentation [https://pkg.go.dev/github.com/google/licensecheck#hdr-Scanning](https://pkg.go.dev/github.com/google/licensecheck#hdr-Scanning) to know how to create `license_regular_expression`.
There is great number of examples in [https://github.com/google/licensecheck/tree/main/licenses](here).
"""
function add_license(id::String, lre::String)
    ccall((:AddLicense, licensecheck_jll.licensecheck),
        Nothing,
        (Cstring, Cstring), id, lre)
    return nothing
end

end # module
